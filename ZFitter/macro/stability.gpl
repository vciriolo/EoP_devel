#### This macro should be called as follows:
#call 'macro/stability.gpl' 'tmp/file1.dat' 'legend 1' 'tmp/file2.dat' 'legend 2' 
reset
#set datafile missing "-"
files=""
titles=""

load 'macro/style.gpl' 
load 'macro/run_periods.gpl'
load 'v.gpl'

set terminal pdfcairo enhanced color font "Helvetica,16" size 5,5
set output 'stability.pdf'

xVar="((column(\"timeMax\")+column(\"timeMin\"))/2)"
xVarDelta="((column(\"timeMax\")-column(\"timeMin\"))/2)"
#xVar="((column(\"runMax\")+column(\"runMin\"))/2)"
#xVarDelta="((column(\"runMax\")-column(\"runMin\"))/2)"
peakLabel="peak estimator "

yVarDelta="((column(\"effSigma\")/sqrt(column(\"nEvents\"))/2)"
#yRes="(column(\"effSigma30\")/column(\"mean68\")*91.2/100)"
yRes="(column(\"effSigmaScaled\")*91.2/100)"
resolutionLabel="effective sigma/peak * MZ [%] "

# Other settings
set timefmt "%s"
set xdata time
set format x "%d/%m"
set link x

set xtics rotate by 45 offset -2., -1.5 nomirror
set x2tics rotate by 30 offset 0, 0 left font ',10' 

set ytics nomirror
set y2tics 0.001 textcolor 'blue'
set autoscale y2

#set x2label 'Run range [..., ...)' offset 0, -1.75
set xlabel 'Time (day/month)' offset 0, -.5

#set ylabel peakLabel
#set y2label resolutionLabel textcolor 'blue'

set auto fix
set offsets graph 0.05, graph 0.05, graph 0.05, graph 0.05
#set y2range [0.010:0.014]

set key outside above
set grid y

#set bmargin 5
set tmargin 6


#load 'data/runRanges/runRangeLimits2.dat.label'

##### The real plots!

if(ARGC==1){
   file=value("ARG".ARGC)
   categories = system("grep '#' ".file." | awk '{print $2}' |  tr '\n' ' ' ")

	do for [category in categories]{
	print category
    set label 1 "Category: ".category noenhanced at screen 0.5, screen 0.92 center

    p for [ yVar in "median" ] file u @xVar:yVar:@xVarDelta:(column("stdDev")/sqrt(column("nEvents"))) i category w xyerr pt 5 ps 0.6 title yVar,\
       '' u @xVar:@yRes:@xVarDelta i category w xerr pt 6 ps 0.6 lc 2 axes x1y2 title 'scaled width'
    }
} else {
	file=value("ARG1")
	categories = system("grep '#' ".file." | awk '{print $2}' |  tr '\n' ' ' ")
	do for[argIndex=1:ARGC]{
       if(argIndex%2==1){
          files=sprintf("%s '%s'", files, value("ARG".argIndex))
        } else{
          titles=sprintf("%s '%s'", titles, value("ARG".argIndex))
        }
	}
    #	print files
    #    print titles
    #	print categories
	unset y2tics
    #  unset y2label
    unset x2tics
    #  set ylabel peakLabel
    set grid y
	do for [category in categories]{
	  	set label 1 "Category: ".category noenhanced at screen 0.5, screen 0.82 center
	 	yVar="median"
		set ylabel VARIABLE.' '.peakLabel.' : '.yVar.' '.peakLabelUnit
		set ytics
		unset y2label
		unset y2tics
	 	p for [id=1:words(files)] word(files,id) u @xVar:yVar:@xVarDelta:(column("stdDev")/sqrt(column("nEvents"))) i category w xyerr pt 5 ps 0.6 title word(titles,id)
        save "tmp/gpl/".category.'.gpl'

#		set y2label VARIABLE.' '.resolutionLabel
#		set y2tics
#		unset ylabel
#		unset ytics
#		p for [id=1:words(files)] word(files,id) u @xVar:@yRes:@xVarDelta i category w xerr pt 6 ps 0.6 axes x1y2 title 'scaled width '.word(titles,id)

	}

}


set output
set term x11


# p for [yVar in "mean mean68"]  file u @xVar:yVar:@xVarDelta:(column("stdDev")/sqrt(0.68*column("nEvents"))):x2ticlabel(stringcolumn("runMin").'-'.stringcolumn("runMax")) i category w xyerr pt 5 ps 0.6 title yVar, \
	   # '' u @xVar:@yRes:@xVarDelta i category w xerr pt 5 ps 0.6 axes x1y2 title 'scaled width'

	
	#   p for [yVar in "(column(\"effSigma\")/column(\"mean68\")*91.2/100)"]  file u @xVar:@yVar:@xVarDelta:x2ticlabel(stringcolumn("runMin").'-'.stringcolumn("runMax")) i category w xerr pt 5 ps 0.6 title yVar
